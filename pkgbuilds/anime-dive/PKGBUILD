# Maintainer: BnSplits <bnsp.x0@gmail.com>

pkgname=anime-dive-git
_pkgname=${pkgname%-git}
pkgver=r59.8b5d0ae
pkgrel=1
pkgdesc="An anime / scans searcher, selector and downloader program for French anime fans"
arch=('x86_64')
url="https://github.com/BnSplits/${_pkgname}"
license=('GPL')
depends=('nodejs' 'chromium' 'aria2' 'n-m3u8dl-re-bin-bn')
makedepends=('git' 'gcc' 'npm')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://github.com/BnSplits/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/${_pkgname}"

  # Install npm dependencies
  npm install --omit=dev

  # Create the C wrapper
  cat >anime-dive.c <<'EOF'
#include <unistd.h>
int main() {
    char *args[] = {"node", "/opt/anime-dive/main.js", NULL};
    execvp("node", args);
    return 1;
}
EOF

  # Compile the wrapper
  gcc -o anime-dive anime-dive.c
}

package() {
  cd "$srcdir/${_pkgname}"

  # Install application files (including node_modules from build)
  mkdir -p "$pkgdir/opt/${_pkgname}"
  cp -r ./* "$pkgdir/opt/${_pkgname}/"

  # Install the binary wrapper
  install -Dm755 anime-dive "$pkgdir/usr/bin/anime-dive"
}
